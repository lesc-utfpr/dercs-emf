plugins {
    id 'java'
    id 'eclipse'
}

group = 'dercs'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    testImplementation libs.junit.api
    testRuntimeOnly libs.junit.runtime

    implementation libs.emf.common
    implementation libs.emf.ecore

    implementation libs.slf4j.api
    runtimeOnly libs.logback

    // something is broken in the intellij gradle importer and
    // using the version catalog here causes it to crash
//    implementation libs.emf.xmi
//
//    implementation (libs.uml2.common) {
//        transitive = false
//    }
//
//    implementation (libs.uml2.types) {
//        transitive = false
//    }
//
//    implementation (libs.emf.ecore2xml) {
//        transitive = false
//    }
//
//    implementation (libs.uml2.uml) {
//        transitive = false
//    }

    // the maven artifacts for uml2 seem to have outdated dependencies
    // so we exclude them and add working ones ourselves
    implementation 'org.eclipse.emf:org.eclipse.emf.ecore.xmi:2.16.0'

    implementation ('org.eclipse.uml2:common:2.0.0-v20140602-0749') {
        transitive = false
    }

    implementation ('org.eclipse.uml2:types:2.0.0-v20140602-0749') {
        transitive = false
    }

    implementation ('org.eclipse.emf.mapping:ecore2xml:2.3.0-v200706262000') {
        transitive = false
    }

    implementation ('org.eclipse.uml2:uml:5.0.0-v20140602-0749') {
        transitive = false
    }

    implementation files("externalLibs/org.eclipse.papyrus.marte.static.profile_1.2.2.jar")
    implementation files("externalLibs/AMoDE-RT.profile.jar")
}

sourceSets {
    main {
        java.srcDirs = ['ECoreDercsModel/src-gen', 'ECoreDercsModel/src', 'src/main/java']
        resources.srcDirs = ['src/main/resources']
    }
}

test {
    useJUnitPlatform()
}

version '1.0-SNAPSHOT'

// for fancy test output
// from: https://stackoverflow.com/a/36130467
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events TestLogEvent.FAILED,
               TestLogEvent.PASSED,
               TestLogEvent.SKIPPED
               //TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events TestLogEvent.STARTED,
                   TestLogEvent.FAILED,
                   TestLogEvent.PASSED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.STANDARD_ERROR
                   //TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}
